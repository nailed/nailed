buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            //Because SpecialSource 1.7 is still beta
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            //MinecraftForge repo for Srg2Source
            name = "forge"
            url = "http://files.minecraftforge.net/maven/"
        }
        /*maven {
            name = "jk-5"
            url = "http://maven.jk-5.tk"
        }*/
        maven {
            name = "offlineRepo"
            url = "http://localhost:81"
        }
    }
    dependencies {
        classpath "jk_5.nailed.mcp:mcp:1.3.2"
    }
}

repositories {
    mavenLocal()
    maven {
        name "sk89q"
        url "http://maven.sk89q.com/repo/"
    }
    /*maven {
        name "jk-5"
        url "http://maven.jk-5.tk"
    }*/
    maven {
        name = "offlineRepo"
        url = "http://localhost:81"
    }
}

apply plugin: "scala"
apply plugin: "java"
apply plugin: "idea"
apply plugin: "maven"
apply plugin: "nailed-mcp"

group = "jk_5.nailed"
version = "1.0.0-SNAPSHOT"

nailedMCP {
    minecraftVersion = "1.8"
    mainClass = "net.minecraft.server.MinecraftServer"
}

configurations {
    deployer
    nailed.exclude module: 'lwjgl'
    nailed.exclude module: 'lwjgl-platform'
    nailed.exclude module: 'jinput'
    nailed.exclude module: 'jinput-platform'
    nailed.exclude module: 'jutils'
}

dependencies {
    nailed 'net.minecraft:launchwrapper:1.11'
    nailed 'org.scala-lang:scala-library:2.11.2'
    nailed 'org.ow2.asm:asm-debug-all:5.0.3'
    nailed 'jk_5.eventbus:EventBus:2.0.1'
    nailed 'lzma:lzma:0.0.1'
    nailed 'com.nothome:javaxdelta:2.0.1'
    nailed 'org.mozilla:rhino:1.7R4'
    nailed 'org.jdom:jdom2:2.0.5'
    nailed 'com.typesafe:config:1.2.1'
    nailed 'org.apache.logging.log4j:log4j-slf4j-impl:2.0.2'
    nailed 'org.javassist:javassist:3.18.2-GA'

    nailed "com.sk89q:worldedit:6.0.0-SNAPSHOT"

    compile project(':api')

    testCompile 'junit:junit:4.12-beta-1'

    deployer 'org.apache.maven.wagon:wagon-ssh:2.6'
}

packageServer.manifest {
    attributes 'TweakClass': 'jk_5.nailed.server.tweaker.NailedTweaker'
    attributes 'Main-Class': 'jk_5.nailed.deploader.DepLoader'
}

/*
 * Nailed, a Minecraft PvP server framework
 * Copyright (C) jk-5 <http://github.com/jk-5/>
 * Copyright (C) Nailed team and contributors <http://github.com/nailed/>
 *
 * This program is free software: you can redistribute it and/or modify it
 * under the terms of the MIT License.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public License
 * for more details.
 *
 * You should have received a copy of the MIT License along with
 * this program. If not, see <http://opensource.org/licenses/MIT/>.
 */

//If only i knew how to add scala sources with the gradle API.
//If you know, please open an issue or message me (jk-5) on irc
sourceSets {
    main {
        scala {
            srcDir 'src/main/scala'
            srcDir 'minecraft/Nailed/src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'minecraft/Nailed/src/main/resources'
        }
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version
        include 'internalplugin.json'
        include 'directorymappackloaderplugin.json'
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'internalplugin.json'
        exclude 'directorymappackloaderplugin.json'
    }
}

uploadArchives {
    if (project.hasProperty("deploymaven")) {
        repositories.mavenDeployer {
            configuration = configurations.deployer
            repository(url: project.deploymaven.url) {
                authentication(userName: project.deploymaven.username, password: project.deploymaven.password)
            }
            pom.project {
                description = 'Nailed, the Minecraft PvP server framework'
                url = 'https://github.com/nailed/nailed'

                scm {
                    url 'https://github.com/nailed/nailed'
                    connection 'scm:git:git://github.com/nailed/nailed.git'
                    developerConnection 'scm:git:git@github.com:nailed/nailed.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/nailed/nailed/issues'
                }

                developers {
                    developer {
                        id = 'jk-5'
                        name = 'jk-5'
                        roles {
                            role 'developer'
                            role 'packager'
                        }
                    }
                    developer {
                        id = 'mattashii'
                        name = 'mattashii'
                        roles {
                            role 'contributor'
                        }
                    }
                }
            }
        }
    }else{
        repositories{
            mavenLocal()
        }
    }
}
