/*
 * Nailed, a Minecraft PvP server framework
 * Copyright (C) jk-5 <http://github.com/jk-5/>
 * Copyright (C) Nailed team and contributors <http://github.com/nailed/>
 *
 * This program is free software: you can redistribute it and/or modify it
 * under the terms of the MIT License.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public License
 * for more details.
 *
 * You should have received a copy of the MIT License along with
 * this program. If not, see <http://opensource.org/licenses/MIT/>.
 */

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            //Because SpecialSource 1.7 is still beta
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            //MinecraftForge repo for Srg2Source
            name = "forge"
            url = "http://files.minecraftforge.net/maven/"
        }
        maven {
            name = "jk-5"
            url = "http://maven.jk-5.nl"
        }
    }
    dependencies {
        classpath "jk_5.nailed.mcp:mcp:1.5.0"
    }
}

evaluationDependsOn('Mixin')

repositories {
    mavenLocal()
    maven {
        name "sk89q"
        url "http://maven.sk89q.com/repo/"
    }
    maven {
        name "jk-5"
        url "http://maven.jk-5.nl"
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "maven"
apply plugin: "nailed-mcp"

group = "jk_5.nailed"
version = "1.1.1-SNAPSHOT"

ext.mixinSrg = new File(project.buildDir, "tmp/mixins/mixins.srg")
ext.mixinRefMap = new File(project.buildDir, "tmp/mixins/mixins.nailed.refmap.json")

nailedMCP {
    minecraftVersion = "1.8"
    mainClass = "net.minecraft.server.MinecraftServer"
    mappings = "snapshot_nodoc_20150227"
}

configurations {
    deployer //Jars needed for maven deployer
    worldedit //Deploy configuration for worldedit
    shadeWorldedit //Libraries shaded into the worldedit jar
    compile.extendsFrom shadeWorldedit

    nailed.exclude module: 'lwjgl'
    nailed.exclude module: 'lwjgl-platform'
    nailed.exclude module: 'jinput'
    nailed.exclude module: 'jinput-platform'
    nailed.exclude module: 'jutils'
}

dependencies {
    nailed 'net.minecraft:launchwrapper:1.11'
    nailed 'org.ow2.asm:asm-debug-all:5.0.3'
    nailed 'jk_5.eventbus:EventBus:2.0.1'
    nailed 'lzma:lzma:0.0.1'
    nailed 'com.nothome:javaxdelta:2.0.1'
    nailed 'org.mozilla:rhino:1.7R4'
    nailed 'org.jdom:jdom2:2.0.5'
    nailed 'com.typesafe:config:1.2.1'
    nailed 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'
    nailed 'org.javassist:javassist:3.18.2-GA'

    shadeWorldedit "com.sk89q:worldedit:6.0.0-SNAPSHOT"

    compile project(':api')
    compile project(':Mixin')

    testCompile 'junit:junit:4.12-beta-1'

    deployer 'org.apache.maven.wagon:wagon-ssh:2.6'
}

packageServer.manifest {
    attributes "TweakClass": "jk_5.nailed.server.tweaker.NailedTweaker"
    attributes "Main-Class": "jk_5.nailed.deploader.DepLoader"
    attributes "Built-By": System.getProperty("user.name")
    attributes "Build-Jdk": System.getProperty("java.version")
    attributes "Created-By": "Gradle " + gradle.gradleVersion
    attributes "Implementation-Version": project.version
    attributes "Implementation-Title": "jk_5.nailed"
    attributes "Implementation-Vendor": "jk-5"
    attributes "Specification-Version": project.version
    attributes "Specification-Title": "Nailed server implementation"
    attributes "Specification-Vendor": "jk-5"
}

//Delay evaluation of packageServer manifest
project.gradle.taskGraph.whenReady { taskGraph ->
    if(taskGraph.hasTask(packageServer)){
        packageServer.manifest{
            attributes("Implementation-Version": project(":api").version, "jk_5.nailed.api")
            attributes("Implementation-Title": "jk_5.nailed.api", "jk_5.nailed.api")
            attributes("Implementation-Vendor": "jk-5", "jk_5.nailed.api")
            attributes("Specification-Version": project(":api").version, "jk_5.nailed.api")
            attributes("Specification-Title": "Nailed api", "jk_5.nailed.api")
            attributes("Specification-Vendor": "jk-5", "jk_5.nailed.api")

            attributes("Implementation-Version": "2.0.0", "jk_5.nailed.deploader")
            attributes("Implementation-Title": "jk_5.nailed.deploader", "jk_5.nailed.deploader")
            attributes("Implementation-Vendor": "jk-5", "jk_5.nailed.deploader")
            attributes("Specification-Version": "2.0.0", "jk_5.nailed.deploader")
            attributes("Specification-Title": "Nailed deploader", "jk_5.nailed.deploader")
            attributes("Specification-Vendor": "jk-5", "jk_5.nailed.deploader")

            attributes("Implementation-Version": project(":Mixin").version, "org.spongepowered.tools")
            attributes("Implementation-Title": "Mixin", "org.spongepowered.tools")
            attributes("Implementation-Vendor": "http://spongepowered.org", "org.spongepowered.tools")
        }
    }
}

compileJava {
    options.compilerArgs += [
            '-Xlint:all',
            '-Xlint:-path',
            '-Xlint:-processing',
            '-Xlint:-unchecked',
            "-AoutSrgFile=${project.mixinSrg.getCanonicalPath()}",
            "-AoutRefMapFile=${project.mixinRefMap.getCanonicalPath()}"
    ]
    options.deprecation = true
    options.encoding = 'UTF-8'
}

afterEvaluate {
    tasks.compileJava.options.compilerArgs += "-AreobfSrgFile=${tasks.reobfuscate.srg}"
}

reobfuscate {
    addExtraSrgFile project.mixinSrg
}

packageServer {
    from(project(":Mixin").sourceSets.main.output){
        exclude("org/spongepowered/tools")
    }
    exclude("jk_5/nailed/plugins/worldedit/**")
}

task worldEditJar(type: Jar) {
    baseName = "WorldEdit"
    version = "1.0.0"

    manifest {
        attributes "Built-By": System.getProperty("user.name")
        attributes "Build-Jdk": System.getProperty("java.version")
        attributes "Created-By": "Gradle " + gradle.gradleVersion
        attributes "Implementation-Version": "1.0.0"
        attributes "Implementation-Title": "jk_5.nailed.plugins.worldedit"
        attributes "Implementation-Vendor": "jk-5"
        attributes "Specification-Version": "1.0.0"
        attributes "Specification-Title": "WorldEdit plugin for Nailed"
        attributes "Specification-Vendor": "jk-5"
    }

    dependsOn "generateBinaryPatches"
    from zipTree("build/tmp/jars/binpatches.jar")
    from {
        configurations.shadeWorldedit.collect {
            it.isDirectory() ? it : zipTree(it).matching {
                exclude "META-INF", "META-INF/**", "*META-INF*", "meta-inf"
                exclude "**.jar", "**/*.jar", "*.jar"
            }
        }
    }
    include("jk_5/nailed/plugins/worldedit/**")
    include("com/sk89q/**")
    include("com/thoughtworks/paranamer/**")
    include("net/royawesome/jlibnoise/**")
}

artifacts {
    worldedit worldEditJar
}

uploadArchives {
    if (project.hasProperty("deploymaven")) {
        repositories.mavenDeployer {
            configuration = configurations.deployer
            repository(url: project.deploymaven.url) {
                authentication(userName: project.deploymaven.username, password: project.deploymaven.password)
            }
            pom.project {
                description = 'Nailed, the Minecraft PvP server framework'
                url = 'https://github.com/nailed/nailed'

                scm {
                    url 'https://github.com/nailed/nailed'
                    connection 'scm:git:git://github.com/nailed/nailed.git'
                    developerConnection 'scm:git:git@github.com:nailed/nailed.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/nailed/nailed/issues'
                }

                developers {
                    developer {
                        id = 'jk-5'
                        name = 'jk-5'
                        roles {
                            role 'developer'
                            role 'packager'
                        }
                    }
                    developer {
                        id = 'mattashii'
                        name = 'mattashii'
                        roles {
                            role 'contributor'
                        }
                    }
                }
            }
        }
    }else{
        repositories{
            mavenLocal()
        }
    }
}

uploadWorldedit {
    if (project.hasProperty("deploymaven")) {
        repositories.mavenDeployer {
            configuration = configurations.deployer
            repository(url: project.deploymaven.url) {
                authentication(userName: project.deploymaven.username, password: project.deploymaven.password)
            }
            pom.groupId = "jk_5.nailed.plugins"
            pom.artifactId = "WorldEdit"
            pom.version = "1.0.0"
            pom.project {
                description = 'WorldEdit plugin for nailed'
                url = 'https://github.com/nailed/nailed'

                scm {
                    url 'https://github.com/nailed/nailed'
                    connection 'scm:git:git://github.com/nailed/nailed.git'
                    developerConnection 'scm:git:git@github.com:nailed/nailed.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/nailed/nailed/issues'
                }

                developers {
                    developer {
                        id = 'jk-5'
                        name = 'jk-5'
                        roles {
                            role 'developer'
                            role 'packager'
                        }
                    }
                    developer {
                        id = 'mattashii'
                        name = 'mattashii'
                        roles {
                            role 'contributor'
                        }
                    }
                }
            }
        }
    }else{
        repositories{
            mavenLocal()
        }
    }
}
