/*
 * Nailed, a Minecraft PvP server framework
 * Copyright (C) jk-5 <http://github.com/jk-5/>
 * Copyright (C) Nailed team and contributors <http://github.com/nailed/>
 *
 * This program is free software: you can redistribute it and/or modify it
 * under the terms of the MIT License.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public License
 * for more details.
 *
 * You should have received a copy of the MIT License along with
 * this program. If not, see <http://opensource.org/licenses/MIT/>.
 */



apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven'

group = 'jk_5.nailed'
version = '1.0.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    deployer
}

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

compileScala {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

dependencies {
    compile 'org.scala-lang:scala-library:2.11.2'
    compile 'jk_5.eventbus:EventBus:2.0.0'
    compile 'org.apache.logging.log4j:log4j-api:2.0.2'
    compile 'io.netty:netty-all:4.0.23.Final'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.code.gson:gson:2.3'

    testCompile 'junit:junit:4.12-beta-1'

    deployer 'org.apache.maven.wagon:wagon-ssh:2.6'
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allSource
    baseName = "nailed-api"

    manifest {
        attributes "Implementation-Version": project.version
        attributes "Implementation-Title": "jk_5.nailed.api"
        attributes "Implementation-Vendor": "jk_5"
        attributes "Specification-Version": project.version
        attributes "Specification-Title": "The api for nailed, the PVP server framework for Minecraft"
        attributes "Specification-Vendor": "jk-5"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task scaladocJar(type: Jar, dependsOn: scaladoc) {
    classifier = 'scaladoc'
    from 'build/docs/scaladoc'
}

artifacts {
    archives jar
    archives sourceJar
    archives scaladocJar
}

uploadArchives {
    if (project.hasProperty("deploymaven")) {
        repositories.mavenDeployer {
            configuration = configurations.deployer
            repository(url: project.deploymaven.url) {
                authentication(userName: project.deploymaven.username, password: project.deploymaven.password)
            }
            pom {
                groupId = 'jk_5.nailed'
                artifactId = 'nailed-api'
            }
            pom.project {
                description = 'The API for Nailed, the Minecraft PvP server framework'
                url = 'https://github.com/nailed/nailed'

                scm {
                    url 'https://github.com/nailed/nailed'
                    connection 'scm:git:git://github.com/nailed/nailed.git'
                    developerConnection 'scm:git:git@github.com:nailed/nailed.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/nailed/nailed/issues'
                }

                developers {
                    developer {
                        id = 'jk-5'
                        name = 'jk-5'
                        roles {
                            role 'developer'
                            role 'packager'
                        }
                    }
                    developer {
                        id = 'mattashii'
                        name = 'mattashii'
                        roles {
                            role 'contributor'
                        }
                    }
                }
            }
        }
    }else{
        repositories{
            mavenLocal()
        }
    }
}
